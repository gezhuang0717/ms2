      Real*8 FUNCTION RDEDX(E1,Z0,A1,Z2,A2,IADJ,I1,I2,I3,RHO,IGAS,ETAD)
C
C  THIS ROUTINE CALCULATES DE/DX USING THE BETHE EQUATION
C  WITH 3 CORRECTION TERMS, THE MOTT, BLOCH, AND LOW VELOCITY Z**3
C  TERMS. (SEE S.P. AHLEN, PRA17,1236(1978)). ANY OR ALL OF THESE
C  CORRECTIONS CAN BE INCLUDED OR IGNORED, SPECIFIED BY THE
C  INDICES I1(MOTT), I2(BLOCH), I3(LOW VELOCITY Z**3).  A ZERO INPUT
C  FOR A GIVEN PARAMETER ELIMINATES THAT PARTICULAR TERM IN THE
C  DE/DX CALCULATION.
C
C  THE SHELL CORRECTIONS ARE TAKEN FROM BARKAS AND BERGER,
C  PUBLICATION 1133 OF THE NATL ACAD SCI.  THE LOW VELOCITY Z**3 CORRECTION
C  IS DERIVED FROM A FIGURE IN MCCARTHY AND JACKSON, PHYS REV B6, 4131 (1972).
C  THE MOTT, BLOCH CORRECTIONS ARE FROM AHLEN>S PREVIOUSLY
C  REFERENCED PAPER.
C
C  UNITS OF E1=MEV/AMU
C  UNITS OF IADJ=EV.  IADJ IS A REAL VARIABLE.
C  UNITS OF RETURNED DE/DX=(MEV/AMU)/(G/CM2)
C
C  F1=STANDARD DE/DX FRONT FACTOR
C  F2=STANDARD BETHE NONRELATIVISTIC TERM WITH SHELL CORRECTIONS
C  F3=BLOCH CORRECTION TERM
C  F4=LOW VELOCITY Z**3 NONRELATIVISTIC CORRECTION FACTOR
C  F5=MOTT CORRECTION TERM (Z**3 TO Z**7)
C  F6=STANDARD BETHE RELATIVISTIC TERM
C
C  RHO=DENSITY OF MATERIAL, G/CM**3 (FOR A GAS, GIVE STANDARD DENSITY)
C  IGAS=0 IF CONDENSED PHASE, 1 IF GAS.
C  ETAD=FOR GAS, DENSITY RELATIVE TO STANDARD (1 ATM, 0 DEG CENT). MUST BE &0.
C
      Implicit Real*8 (A-H,O-Z)
      Parameter (PI=3.1415926535)
      COMMON/FLOOK/F1,F2,F3,F4,F5,F6,Z1
      REAL*8 IADJ
      DIMENSION VA(4),V2FVA(4),Z1ABA(14),COSXA(14)
      DATA Z1ABA,COSXA/0.0,0.05,0.1,0.15,0.20,0.30,0.4,0.5,0.6,
     C  0.8,1.0,1.2,1.5,2.0,1.000,0.9905,0.9631,0.9208,0.8680,
     C  0.7478,0.6303,0.5290,0.4471,0.3323,0.2610,
     C  0.2145,0.1696,0.1261/
      DATA VA,V2FVA/1.,2.,3.,4.,0.33,0.30,0.26,0.23/
      ALPHA=1./137.03604
      G=1.+E1/931.5016
      DELT=DELTA(G,Z2,A2,IADJ,RHO,IGAS,ETAD)
      BSQ=1.-1./G**2
      B=DSQRT(BSQ)
      Z1=Z0*(1.-DEXP(-130.*B/Z0**(2./3.)))
      ETA=B*G
      EMASS=0.5110034E+06
      F1=0.3070722*Z1**2*Z2/(BSQ*A2)
      ETAM2=1./ETA**2
      CADJ=1.0E-06*IADJ**2*ETAM2*(0.422377+ETAM2*(0.0304043-ETAM2*
     1   0.00038106))+1.0E-09*IADJ**3*ETAM2*(3.858019+ETAM2*(-0.1667989
     2   +ETAM2*0.00157955))
      F2=DLOG(2.*EMASS*BSQ/IADJ)-CADJ/Z2
      F6=2.*DLOG(G)-BSQ
      F3=0.0
      F4=1.0
      F5=0.0
C
C  IF I2=0, DO NOT CALCULATE BLOCH CORRECTION.
      IF(I2.EQ.0)GO TO 60
      Y=Z1*ALPHA/B
      Y2=Y**2
      MSUM=INT(5.*Y)+1
      SUMR=0.
      DO 90 N=1,MSUM
      FN=dble(N)
      FN2=FN**2
90    SUMR=SUMR+(1./(FN2+Y2)-1./FN2)/FN
      F3=-Y2*(1.202+SUMR)
C
C  IF I3=0, DO NOT CALCULATE LOW VELOCITY CORRECTION.
60    IF(I3.EQ.0)GO TO 50
      V=ETA/(ALPHA*DSQRT(Z2))
      IF(V.GE.4.)GO TO 25
      DO 10 I=1,3
      IF(V.GE.VA(I+1))GO TO 10
      V2FV=V2FVA(I)+(V-VA(I))*(V2FVA(I+1)-V2FVA(I))
      GO TO 30
10    CONTINUE
25    V2FV=0.45/DSQRT(V)
30    F4=1.+2.*Z1*V2FV/(V**2*DSQRT(Z2))
C
C  IF I1=0, DO NOT CALCULATE MOTT CORRECTION.
50    IF(I1.EQ.0)GO TO 70
      Z1A=Z1*ALPHA
      Z1AB=ABS(Z1A/B)
      COSX=0.
      DO 40 I=1,13
      IF(Z1AB.GE.Z1ABA(I+1))GO TO 40
      COSX=COSXA(I)+(Z1AB-Z1ABA(I))*(COSXA(I+1)-COSXA(I))/
     C        (Z1ABA(I+1)-Z1ABA(I))
40    CONTINUE
      F5=0.5*Z1A*(B*(1.725+0.52*PI*COSX)+Z1A*(3.246-0.451*BSQ
     1   +Z1A*(1.522*B+0.987/B+Z1A*(4.569-0.494*BSQ-2.696/BSQ
     2    +Z1A*(1.254*B+0.222/B-1.170/BSQ/B)))))
      IF(Z1AB.LE.100.*ALPHA)GO TO 70
      IF((Z1AB**9/6.).LT.ABS(F5/(F2*F4+F3+F5+F6-DELT/2.)))GO TO 70
      F5=0.
70    RDEDX=F1*(F2*F4+F3+F5+F6-DELT/2.)/A1
      RETURN
      END

      Real*8 FUNCTION DELTA(G,Z2,A2,FIADJ,RHO,IGAS,ETA)
C  THIS FUNCTION IS USED BY RDEDX.
C
C  THIS CORRECTION FOR THE DENSITY EFFECT IS BASED ON STERNHEIMER AND
C  PEIERLS, PHYS REV B3, 3681 (1971).
C  SET IGAS= 0 OR 1,  ETA & 0 REQUIRED.
C  RHO IS DENSITY IN G/CM**3.  FOR A GAS, GIVE RHO AT T=0 DEGREES
C  CENTIGRADE, 1 ATM PRESSURE, AND THE FACTOR ETA WHICH GIVES THE
C  ACTUAL GAS DENSITY UPON MULTIPLICATION BY RHO.
      Implicit Real*8 (A-H,O-Z)
      IF(G.GE.1.8)GO TO 10
      DELTA=0.
      RETURN
10    PLASMA=28.8*DSQRT(RHO*Z2/A2)
      CBAR=2.*DLOG(FIADJ/PLASMA)+1.0
      B=DSQRT(1.-1./G**2)
      Y=2.*DLOG(B*G)+IGAS*DLOG(ETA)
      IF(IGAS.EQ.1)GO TO 100
      IF(FIADJ.GE.100.)GO TO 20
      Y1=9.212
      IF(CBAR.GE.3.681)GO TO 11
      Y0=0.9212
      GO TO 200
11    Y0=1.502*CBAR-4.606
      GO TO 200
20    Y1=13.82
      IF(CBAR.GE.5.215)GO TO 21
      Y0=0.9212
      GO TO 200
21    Y0=1.502*CBAR-6.909
      GO TO 200
100   IF(CBAR.GE.12.25)GO TO 110
      Y1=18.42
      IF(CBAR.LT.12.25)Y0=9.212
      IF(CBAR.LT.11.5)Y0=8.751
      IF(CBAR.LT.11.0)Y0=8.291
      IF(CBAR.LT.10.5)Y0=7.830
      IF(CBAR.LT.10.0)Y0=7.370
      GO TO 200
110   Y1=23.03
      IF(CBAR.GE.13.804)GO TO 120
      Y0=9.212
      GO TO 200
120   Y0=1.502*CBAR-11.52
200   A=(CBAR-Y0)/(Y1-Y0)**3
      IF(Y.GT.Y0)GO TO 210
      DELTA=0.
      RETURN
210   IF(Y.GE.Y1)GO TO 220
      DELTA=Y-CBAR+A*(Y1-Y)**3
      RETURN
220   DELTA=Y-CBAR
      RETURN
      END

      Real*8 FUNCTION 
     &     RRANGE(E,Z1,A1,Z2,A2,IADJ,I1,I2,I3,RHO,IGAS,ETAD)
C  THIS ROUTINE RETURNS RANGE IN G/CM2 FOR INPUT# E IN MEV/AMU!
C  Z1,Z2 CHARGE OF ION AND MEDIUM! A1,A2 IN AMU! IADJ IN EV.
C  MAXIMUM E= 3 GEV/AMU
C  THE RANGE IS CALCULATED USING THE RDEDX FUNCTION WHICH RETURNS
C  THE BETHE DE/DX WITH MOTT, BLOCH, AND LOW VEL Z**3 CORRECTIONS
C  ADDED. ANY OF THESE CORRECTIONS MAY BE DELETED BY SPECIFYING
C  I1(MOTT), I2(BLOCH), I3(LOW VELOCITY Z**3) EQUAL TO ZERO.
C
C  ALONG WITH THE PARTICULAR VALUE OF RANGE REQUESTED, AN ARRAY
C  IS RETURNED IN COMMON/RCMMN/, TRANGE, WHICH GIVES RRANGE(E) FOR
C  E TABULATED IN COMMON ARRAY TENERG.
C
C  AFTER THE FIRST CALL, IF PARAMETERS OTHER THAN E DO NOT CHANGE,
C  LINEAR INTERPOLATION IS USED TO CALCULATE RRANGE USING THE
C  PREVIOUSLY CALCULATED TRANGE TABLE.
C  THE RANGE TABLES FOR UP TO 20 DIFFERENT MEDIA (WITH SAME ION
C  SPECIE) CAN BE CALCULATED AND STORED FOR SUBSEQUENT CALLS.  THIS
C  FACILITATES STACK CALCULATIONS. IF EXTERNAL USE OF THE RANGE-
C  ENERGY TABLES IS REQUIRED, THE EXTERNAL PROGRAM MUST INCLUDE
C  COMMON/PARAC/ IN ORDER TO CORRELATE THE PARTICULAR IADJ, Z2, A2
C  WITH THE APPROPRIATE TRANGE VECTOR.
C
      Implicit Real*8 (A-H,O-Z)
      REAL*8 IADJ,IADJA(20)
      COMMON/PARAC/Z1P,A1P,I1P,I2P,I3P,NIADJ,IADJA,Z2A(20),A2A(20)
     1      ,RHOA(20),IGASA(20),ETADA(20)
      COMMON/RCMMN/TENERG(138),TRANGE(138,20)
      common/rcmmn2/MIADJ
c      COMMON/RCMMN/MIADJ,TENERG(138),TRANGE(138,20)
      DATA TENERG/1.,2.,3.,4.,5.,6.,7.,8.,9.,10.,12.,14.,16.,18.,
     C  20.,22.,24.,26.,28.,30.,32.,34.,36.,38.,40.,42.,44.,46.,
     C  48.,50.,55.,60.,65.,70.,75.,80.,85.,90.,95.,100.,105.,
     C  110.,115.,120.,125.,130.,135.,140.,145.,150.,155.,160.,
     C  165.,170.,175.,180.,185.,190.,195.,200.,210.,220.,230.,
     C  240.,250.,260.,270.,280.,290.,300.,310.,320.,330.,340.,
     C  350.,360.,370.,380.,390.,400.,410.,420.,430.,440.,450.,
     C  460.,470.,480.,490.,500.,510.,520.,530.,540.,550.,560.,
     C  570.,580.,590.,600.,610.,620.,630.,640.,650.,660.,670.,
     C  680.,690.,700.,710.,720.,730.,740.,750.,760.,770.,780.,
     C  790.,800.,820.,840.,860.,880.,900.,920.,940.,960.,980.,
     C  1000.,1200.,1400.,1600.,1800.,2000.,2400.,2800.,3200./
      IF((Z1.EQ.Z1P).AND.(A1.EQ.A1P).AND.
     C  (I1.EQ.I1P).AND.
     C  (I2.EQ.I2P).AND.(I3.EQ.I3P))GO TO 451
C  PARAMETERS HAVE CHANGED.  GENERATE THE FIRST OF A NEW SET OF TABLES.
      NIADJ=1
      MIADJ=1
      Z1P=Z1
      A1P=A1
      I1P=I1
      I2P=I2
      I3P=I3
      IADJA(1)=IADJ
      Z2A(1)=Z2
      A2A(1)=A2
      RHOA(1)=RHO
      IGASA(1)=IGAS
      ETADA(1)=ETAD
      GO TO 450
C  PARAMETERS SAME, BUT IS THIS A NEW IADJ,Z2,A2,RHO,IGAS,ETAD'
451   DO 420 I=1,NIADJ
      MIADJ=I
C  IF NOT, GO DIRECTLY TO INTERPOLATION SECTION.
      IF((IADJ.EQ.IADJA(I)).AND.(Z2.EQ.Z2A(I)).AND.
     1  (A2.EQ.A2A(I)).AND.(RHO.EQ.RHOA(I)).AND.(IGAS.EQ.IGASA(I)).
     2   AND.(ETAD.EQ.ETADA(I)))GO TO 100
420   CONTINUE
C  IF SO, GENERATE NEXT IADJ TABLE.
      NIADJ=NIADJ+1
      IF(NIADJ.GE.20)NIADJ=20
      MIADJ=NIADJ
      IADJA(MIADJ)=IADJ
      Z2A(MIADJ)=Z2
      A2A(MIADJ)=A2
      RHOA(MIADJ)=RHO
      IGASA(MIADJ)=IGAS
      ETADA(MIADJ)=ETAD
C  FOR E { 9 MEV/AMU, WE USE AN EMPIRICAL FIT TO PROTON RANGE DATA
C  GIVEN BY BARKAS AND BERGER.
C  THE USE OF Z1EFF IN RDEDX AUTOMATICALLY INCORPORATES RANGE
C  EXTENSION DUE TO ELECTRON PICKUP IN THE RANGE CALCULATION FOR E&8
C  MEV/AMU.  FOR E{9 MEV/AMU, WHERE THE ANALYTIC FORM FOR THE SHELL CORRECTION
C  FAILS AND AN EMPIRICAL FIT TO PROTON RANGE DATA IS USED, A RANGE EXTENSION
C  GIVEN BY BARKAS + BERGER IN THIS REGION IS USED.
450   DO 30 I=1,8
30    TRANGE(I,MIADJ)=(A1*(931.5016/938.213)/Z1**2)*PRNGLO(TENERG(I),
     1     IADJ,Z2,A2)+BZ(TENERG(I),Z1,A1,Z2,A2,IADJ)
C  THIS SECTION INTEGRATES 1./DE/DX TO OBTAIN RANGE. THE METHOD IS
C  THAT OF GAUSS-LEGENDRE WITH 4 SAMPLE POINTS FOR INTEGRATION, THE
C  RANGES BEING INTEGRATD FROM TABULATED POINT TO TABULATED POINT.
      DO 300 I=9,138
      DE2=(TENERG(I)-TENERG(I-1))/2.
C
      DEDX1=RDEDX((TENERG(I-1)+1.33998104*DE2),Z1,A1,Z2,A2,IADJ,
     C    I1,I2,I3,RHO,IGAS,ETAD)
      DEDX2=RDEDX((TENERG(I-1)+1.86113631*DE2),Z1,A1,Z2,A2,IADJ,
     C    I1,I2,I3,RHO,IGAS,ETAD)
      DEDX3=RDEDX((TENERG(I-1)+0.13886369*DE2),Z1,A1,Z2,A2,IADJ,
     C    I1,I2,I3,RHO,IGAS,ETAD)
      DEDX4=RDEDX((TENERG(I-1)+0.66001869*DE2),Z1,A1,Z2,A2,IADJ,
     C    I1,I2,I3,RHO,IGAS,ETAD)
      DR=DE2*(0.65214515/DEDX1 + 0.34785485/DEDX2
     C       +0.34785485/DEDX3 + 0.65214515/DEDX4)
300   TRANGE(I,MIADJ)=TRANGE(I-1,MIADJ)+DR
100   IF(E.GT.TENERG(1))GO TO 60
      RRANGE=E*TRANGE(1,MIADJ)/TENERG(1)
      RETURN
60    DO 70 I=2,138
      IF(E.GT.TENERG(I))GO TO 70
        EE1=TENERG(I-1)        
        EE2=TENERG(I)        
        EE3=TENERG(I+1)        
        R1=TRANGE(I-1,MIADJ)
        R2=TRANGE(I,MIADJ)
        R3=TRANGE(I+1,MIADJ)
        DD=EE3*EE3*(EE2-EE1)+EE2*EE2*(EE1-EE3)+EE1*EE1*(EE3-EE2)
        AA=(R3*(EE2-EE1)+R2*(EE1-EE3)+R1*(EE3-EE2))/DD
        BB=(R3*(EE1*EE1-EE2*EE2)+R2*(EE3*EE3-EE1*EE1)
     &                              +R1*(EE2*EE2-EE3*EE3))/DD
        CC=(R3*EE2*EE1*(EE2-EE1)+R2*EE1*EE3*(EE1-EE3)
     &                              +R1*EE3*EE2*(EE3-EE2))/DD
        RRANGE=AA*E*E+BB*E+CC
C        type *,'AA= ',AA,'BB= ',BB,'CC= ',CC
C        TYPE *,'RRANGE= ',RRANGE
C      RRANGE=TRANGE(I-1,MIADJ)+(E-TENERG(I-1))
C     C    *(TRANGE(I,MIADJ)-TRANGE(I-1,MIADJ))
C     C   /(TENERG(I)-TENERG(I-1))
      GO TO 200
70    CONTINUE
200   CONTINUE
      RETURN
      END

      Real*8 FUNCTION BZ(E,Z1,A1,Z2,A2,IADJ)
C  THIS FUNCTION IS USED BY RRANGE.
C
C  THIS ROUTINE RETURNS THE TOTAL RANGE EXTENSION TO RANGE
C  AS GIVEN BY BARKAS AND BERGER. THE A1/Z1**2 FACTOR IS
C  INCLUDED WITHIN THE ROUTINE, SO THE RETURNED VALUE IS TO BE
C  ADDED DIRECTLY TO THE CONSTANT CHARGE STATE RANGE.
C
C  UNITS OF BZ=G/CM2, E=MEV/AMU.
C
      Implicit Real*8 (A-H,O-Z)
      REAL*8 IADJ
      G=1.+E/931.5016
      B=DSQRT(1.-1./G**2)
      IF(B.GE.(2.*Z1/137.))GO TO 10
      BZ=A1*(931.481/938.259)*(48.0+5.8*IADJ**(5./8.))*
     C     (A2/Z2)*1.0E-05*Z1**(-1./3.)*B
      GO TO 20
10    BZ=(A1*(931.481/938.259))*(7.0+0.85*IADJ**(5./8.))*
     C    (A2/Z2)*1.0E-06*Z1**(2./3.)
20    CONTINUE
      RETURN
      END

      double precision FUNCTION PRNGLO(E,IADJ,Z2,A2)
C  THIS FUNCTION IS USED BY RRANGE.
C
C  THIS ROUTINE (PROTON-RANGE-LOW) CALCULATES PROTON RANGES
C  FOR 1{E{9 MEV/AMU. THIS USES THE EMPIRICAL RANGE EXPRESSION
C  BY BARKAS AND BERGER . RETURNED RANGE IS IN G/CM2. IT MUST
C  BE MULTIPLIED BY A1*(931.5016/938.213)/Z1**2 FOR CHARGES HIGHER
C  THAN 1. A1 IS IN AMU.
C
      Implicit Real*8 (A-H,O-Z)
      REAL*8 IADJ
      CR=938.213/931.5016
      ELN=DLOG(E*CR)
      ALN=DLOG(IADJ)
      RLN=DLOG(A2/Z2)-7.5265E-01+2.5398*ELN-2.4598E-01*ELN**2
     C  +7.3736E-02*ALN-3.1200E-01*ELN*ALN
     C  +1.1548E-01*ALN*ELN**2+4.0556E-02*ALN**2
     C  +1.8664E-02*ALN**2*ELN-9.9661E-03*ALN**2*ELN**2
      PRNGLO=DEXP(RLN)/1.0d+03
      RETURN
      END

      Real*8 FUNCTION 
     &         RNERGY(R,Z1,A1,Z2,A2,IADJ,I1,I2,I3,RHO,IGAS,ETAD)
C  THIS ROUTINE IS THE COUNTERPART OF RRANGE, THE CORRECTED RANGE.
C  RRANGE IS CALLED, PRODUCING A RANGE-ENERGY TABLE. RNERGY IS
C  DETERMINED BY LINEAR INTERPOLATION ON THIS TABLE.
C
C  UNITS# R=G/CM2
C         A1,A2=AMU
C         IADJ=EV
C         RNERGY=MEV/AMU
C
      Implicit Real*8 (A-H,O-Z)
      REAL*8 IADJ, IADJA(20)
      COMMON/PARAC/Z1P,A1P,I1P,I2P,I3P,NIADJ,IADJA,Z2A(20),A2A(20)
     1      ,RHOA(20),IGASA(20),ETADA(20)
      COMMON/RCMMN/TENERG(138),TRANGE(138,20)
      common/rcmmn2/MIADJ
c      COMMON/RCMMN/MIADJ,TENERG(138),TRANGE(138,20)
C  PICK A VALUE OF E IN CALLING RRANGE
      E=600.
C  THIS CONSTRUCTS THE RANGE-ENERGY TABLE TO BE USED BELOW IF ONE
C  DOES NOT ALREADY EXIST.
      RR=RRANGE(E,Z1,A1,Z2,A2,IADJ,I1,I2,I3,RHO,IGAS,ETAD)
      IF(R.LE.TRANGE(1,MIADJ))GO TO 20
      DO 10 I=2,138
      K=I
      IF(R.LE.TRANGE(I,MIADJ))GO TO 30
10    CONTINUE
30    CONTINUE 
C        RNERGY=TENERG(K-1)+(R-TRANGE(K-1,MIADJ))*(TENERG(K)-TENERG(K-1))/
C     C                (TRANGE(K,MIADJ)-TRANGE(K-1,MIADJ))
        EE1=TENERG(I-1)        
        EE2=TENERG(I)        
        EE3=TENERG(I+1)        
        R1=TRANGE(I-1,MIADJ)
        R2=TRANGE(I,MIADJ)
        R3=TRANGE(I+1,MIADJ)
        DD=EE3*EE3*(EE2-EE1)+EE2*EE2*(EE1-EE3)+EE1*EE1*(EE3-EE2)
        AA=(R3*(EE2-EE1)+R2*(EE1-EE3)+R1*(EE3-EE2))/DD
        BB=(R3*(EE1*EE1-EE2*EE2)+R2*(EE3*EE3-EE1*EE1)
     &                              +R1*(EE2*EE2-EE3*EE3))/DD
        CC=(R3*EE2*EE1*(EE2-EE1)+R2*EE1*EE3*(EE1-EE3)
     &                              +R1*EE3*EE2*(EE3-EE2))/DD
        RNERGY=(-BB+DSQRT(BB*BB-4.*AA*(CC-R)))/2./AA
C        type *,'rnergy=   ',rnergy
      RETURN
20    RNERGY=TENERG(1)*R/TRANGE(1,MIADJ)
C  OBVIOUSLY, THIS FUNCTION IS INACCURATE BELOW E=1 MEV/AMU
C        type *,'rnergy=   ',rnergy
      RETURN
      END
