* Created       : Thu, 10 May 2012 17:02:26 +0900, by nagashima
* Last Modified : Sun, 20 May 2012 19:36:48 +0900, by ana

macro glfit hist min max
	* Linear + Gaussian fitting macro.
	* Initial values are required in this code.
	IF ([3].eq.'') THEN
		mess 'Error: Invalid number of arguments.'
		exitm
	endif

	pos = $INDEX([min], '.')
	if ([pos].eq.0) then
		min = [min].
	endif

	pos = $INDEX([max], '.')
	if ([pos].eq.0) then
		max = [max].
	endif


	* Edit following parameters. 
	*   f(x) = a + b*x + A exp(-(x - mu)^2/( 2 sigma^2)) 
	* Initial, Min, Max and Step should be specified for each parameter.
	itrcpt_init = 11000.0     ; itrcpt_min = 5000.0     ; itrcpt_max = 15000.0     ; itrcpt_step = 1.0
	slope_init = -10.7     ; slope_min = -20.0     ; slope_max = 0.0     ; slope_step = 0.01
	A_init = 800.0     ; A_min = 500.0     ; A_max = 1000.0     ; A_step = 0.1
	mu_init = 1507.0    ; mu_min = 1500.0    ; mu_max =1520.0    ; mu_step = 0.01
	sigma_init = 3.0 ; sigma_min = 2.65 ; sigma_max = 3.2 ; sigma_step = 0.01
	* Double Gaussian mode
	* A2_init = 0.0     ; A2_min = 0.0     ; A2_max = 0.0     ; A2_step = 0.0
	* mu2_init = 0.0    ; mu2_min = 0.0    ; mu2_max = 0.0    ; mu2_step = 0.0
	* sigma2_init = 0.0 ; sigma2_min = 0.0 ; sigma2_max = 0.0 ; sigma2_step = 0.0

	vector/create par(50) ; vect/create pare(50) 
	vector/create pmin(50) ; vect/create pmax(50)
	vector/create step(50)
	vector/input  PAR(1:2)  [itrcpt_init] [slope_init]
	vector/input PMIN(1:2)  [itrcpt_min]  [slope_min]
	vector/input PMAX(1:2)  [itrcpt_max]  [slope_max]
	vector/input STEP(1:2)  [itrcpt_step] [slope_step]
	vector/input   PAR(3:5) [A_init] [mu_init] [sigma_init]
	vector/input  PMIN(3:5) [A_min]  [mu_min]  [sigma_min]
	vector/input  PMAX(3:5) [A_max]  [mu_max]  [sigma_max]
	vector/input  STEP(3:5) [A_step] [mu_step] [sigma_step]
	* veorct/input   PAR(1:3) [A_init] [mu_init] [sigma_init]
	* vect/input  PMIN(1:3) [A_min]  [mu_min]  [sigma_min]
	* vect/input  PMAX(1:3) [A_max]  [mu_max]  [sigma_max]
	* vect/input  STEP(1:3) [A_step] [mu_step] [sigma_step]
	* vect/input   PAR(6:8) [A2_init] [mu2_init] [sigma2_init]
	* vect/input  PMIN(6:8) [A2_min]  [mu2_min]  [sigma2_min]
	* vect/input  PMAX(6:8) [A2_max]  [mu2_max]  [sigma2_max]
	* vect/input  STEP(6:8) [A2_step] [mu2_step] [sigma2_step]

	* single Gaussian
	histogram/fit [hist]([min]:[max]) P1+G B 5 PAR step(1:5) pmin pmax PARE(1:5)
	* Double Gaussian
	* hist/fit [hist]([min]:[max]) P1+G B 5 PAR step(1:5) pmin pmax PARE(1:5)

	* Output result
	itrcpt = par(1) ; itrcpte = pare(1)
	slope  = par(2) ; slopee  = pare(2)
	amp    = par(3) ; ampe    = pare(3)
	mean   = par(4) ; meane   = pare(4)
	sigma  = par(5) ; sigmae  = pare(5)
	* amp    = par(1) ; ampe    = pare(1)
	* mean   = par(2) ; meane   = pare(2)
	* sigma  = par(3) ; sigmae  = pare(3)
	fwhm   = [sigma]*2.355 ; fwhme = [sigmae] * 2.355
	message '  Amp    = ' $format([amp],F7.3) '+-' $format([ampe],F7.3)
	message '  Mean   = ' $format([mean],F7.3) '+-' $format([meane],F7.3)
	message '  Sigma  = ' $format([sigma], F7.3) '+-' $format([sigmae], F7.3)
	message '  FWHM   = ' $format([fwhm],F7.3) '+-' $format([fwhme],F7.3)
	message '  B.G.   = ' $format([itrcpt],F7.3) '+' $format([slope],F8.5) '*x'
	* mess '  B.G. Itrcept = ' $format([itrcpt],F7.3) '+' $format([itrcpte],F8.5) '*x'
	* mess '       Slope   = ' $format([slope],F7.3) '+' $format([slopee],F8.5) '*x'


	 hi/pl [hist]([min]:[max])
	
	vector/delete par
	vector/delete pare
	vector/delete step
	vector/delete pmin
	vector/delete pmax
return
